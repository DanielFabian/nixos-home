# HP ZBook Studio x360 G5 - Firmware Configuration
{
  config,
  pkgs,
  inputs,
  lib,
  ...
}:

{
  imports = [
    ./hardware-configuration.nix # generated by nixos-generate-config
    ../../modules/firmware/boot.nix
    ../../modules/firmware/zfs.nix
    ../../modules/firmware/snapshots.nix
    ../../modules/firmware/nvidia.nix
    ../../modules/firmware/virtualization.nix
    ../../modules/firmware/networking.nix
    ../../modules/firmware/flatpak.nix
    ../../modules/desktop/hyprland.nix
    ../../modules/desktop/steam.nix # Gaming
    # ../../modules/desktop/plasma.nix # Not needed - portals work without full Plasma
    # ../../modules/desktop/cosmic.nix # System76 DE (alpha) - disabled for now
    ../../modules/desktop/niri.nix # scrollable-tiling WM (primary)
    ../../modules/users/dany.nix
  ];

  networking.hostName = "zbook";
  networking.hostId = "8425e349"; # required for ZFS - keep from old config

  # Firmware philosophy: set once, forget
  time.timeZone = "Europe/London";

  # Keyboard: ISO UK + Colemak Mod-DH
  # `console.keyMap` is for kbd keymaps (loadkeys) and doesn't understand XKB variants.
  # Console derives keymap from XKB via useXkbConfig.
  services.xserver.xkb = {
    model = "pc105"; # ISO keyboard
    layout = "gb"; # UK
    variant = "colemak_dh"; # Colemak Mod-DH (xkeyboard-config name)
    options = "caps:escape";
  };

  console = {
    earlySetup = true;
    useXkbConfig = true;
  };

  # Nix settings
  nix = {
    settings = {
      experimental-features = [
        "nix-command"
        "flakes"
      ];
      auto-optimise-store = true;
    };
    gc = {
      automatic = true;
      dates = "weekly";
      options = "--delete-older-than 30d";
    };
  };

  # Firmware packages - stable, boring, essential
  environment.systemPackages = with pkgs; [
    # Absolute basics
    git
    vim # emergency editor, lazyvim is in home-manager
    wget
    curl
    htop
    btop

    # Filesystem tools
    zfs
    cryptsetup

    # Hardware diagnostics
    pciutils
    usbutils
    lshw

    # Network diagnostics
    iproute2
    dnsutils

    # Secure boot tooling
    sbctl
  ];

  # Printing - firmware level, stable
  services.printing.enable = true;

  # Sound - pipewire (firmware, Wayland-native)
  security.rtkit.enable = true;
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;
  };

  # Bluetooth
  hardware.bluetooth.enable = true;

  system.stateVersion = "25.11";
}
